*** Settings ***
Suite Setup       SuiteSetup
Suite Teardown    SuiteTeardown
Test Setup        Empty Cart
Default Tags      sanity
Resource          ../GlobalKeywords/StringExtensions.txt
Resource          ../PagesKeywords/Header.txt
Resource          ../PagesKeywords/SearchResults.txt
Resource          ../PagesKeywords/PDP.txt
Resource          ../PagesKeywords/Wishlist.txt
Resource          ../PagesKeywords/My_account.txt

*** Variables ***
${searchFor}      Tequila

*** Test Cases ***
Add single product to Cart
    [Tags]    smoke
    Search for products    ${searchFor}
    Click search result item    2
    Should be on PDP page
    ${pdpName}=    PDP.Get product Name
    Add product to Cart
    Open Cart
    Should be on Cart page
    Product name should be    ${pdpName}
    Products number should be    1
    Product quantity should be    1

Add several product qty to Cart
    Search for products    ${searchFor}
    Click search result item    2
    Should be on PDP page
    ${pdpName}=    PDP.Get product Name
    Set product QTY    3
    Add product to Cart
    Open Cart
    Should be on Cart page
    Product name should be    ${pdpName}
    Products number should be    1
    Product quantity should be    3
